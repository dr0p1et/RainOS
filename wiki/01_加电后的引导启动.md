# 第一课：加电后的引导启动

bootloader的作用就是把操作系统内核代码加载到内存中，并执行起来。

为了兼容性考虑，现代操作系统虽然已经是32位或64位，但启动时默认都只是处于16位实模式。设备加电后，BIOS代码会把磁盘的第一个扇区共512字节加载到内存的0x7c00处并跳转执行。

因此我们需要在这个512字节的空间中编写一段程序，完成操作系统加载到内存、开启32位和64位的保护模式、记录硬件参数等功能，另外还需要将整个磁盘需要指定为一个文件系统，就需要从文件系统中找出并读取操作系统代码。还有磁盘分区的功能。

完成整个操作系统程序加载的代码就是bootloader。

因此512字节一般无法完成所有的工作，通常分为512字节的boot和更大的loader程序。

512字节的boot负责把loader加载到内存，loader完成其余的功能。

## 1. boot程序

512字节的boot程序，需要把loader加载到物理内存。

```
#    boot.s
# 磁盘第一个512字节的主引导扇区，被BIOS加载到0x7c00处
# 用于加载之后4个扇区的setup代码

BOOTSEG  = 0x07c0
INITSEG  = 0x9000

SETUPSEG = 0x9020
SETUPLEN = 4

SYSSEG   = 0x1000
SYSSIZE  = 0x3000
ENDSEG   = SYSSEG + SYSSIZE

entry start

# 第一步：将boot引导扇区512字节拷贝并跳转到0x90000处
start:
    mov ax, #BOOTSEG
    mov ds, ax
    mov ax, #INITSEG
    mov es, ax
    mov cx, #256
    sub si, si
    sub di, di
    rep
    movw                 ! 将DS:SI的内容拷贝至ES:DI
    jmpi go, INITSEG    ! 段间跳转 jmpi 段内偏移, 段基址

go:
    mov ax, cs  ! cs=0x9000
    mov ds, ax
    mov es, ax
    mov ss, ax
    mov sp, #0xff00

# 第二步：加载1-5共4个扇区的setup代码
load_setup:
    mov dx, #0x0080 ! DL＝驱动器 00H~7FH软盘 80H~0FFH硬盘
    mov cx, #0x0002
    mov bx, #0x0200
    mov ax, #0x0200 + SETUPLEN
    int 0x13
    jnc ok_load_setup  ! 如果出错则CF标志置位 ah出错码

re_load:
    xor dl, dl
    xor ah, ah
    int 0x13
    j load_setup

ok_load_setup:
    mov di, #msg
    mov si, #msg_size
    call printk

    jmpi 0, SETUPSEG

# boot程序到此结束，下面是子程序和扇区标志

! 读取光标位置 结果存储在dx
! printk(void *di:msg, uint16 *si:size)
printk:
    mov ah, #0x03  ! 读取光标位置
    xor bh, bh
    int 0x10

    ! 显示字符
    mov bp, di
    mov cx, si  ! size

    mov bx, #0x0007
    mov ax, #0x1301
    int 0x10
    ret

msg:
    .ascii "Loading RainOS..."
msg_size:
    .word 0x0011

.org 510
boot_flag:
    .word 0xAA55
```

这一部分很简单，第一步将boot拷贝到0x90000处继续执行，第二步加载后面的4个扇区内容。

编译：

bximage新建一个flat格式的硬盘：

```
# bximage

1. Create new floppy or hard disk image
2. Convert hard disk image to other format (mode)
3. Resize hard disk image
4. Commit 'undoable' redolog to base image
5. Disk image info

0. Quit

Please choose one [0] 1

Create image

Do you want to create a floppy disk image or a hard disk image?
Please type hd or fd. [hd]

What kind of image should I create?
Please type flat, sparse, growing, vpc or vmware4. [flat] flat

Choose the size of hard disk sectors.
Please type 512, 1024 or 4096. [512]

Enter the hard disk size in megabytes, between 10 and 8257535
[10] 10

What should be the name of the image?
[c.img] hd.img

Creating hard disk image 'hd.img' with CHS=20/16/63 (sector size = 512)

The following line should appear in your bochsrc:
  ata0-master: type=disk, path="hd.img", mode=flat
(The line is stored in your windows clipboard, use CTRL-V to paste)

Press any key to continue
```

Makefile编译并用dd写入到生成的hd.img硬盘文件中。

```
boot:
    as86 -0 -b boot.bin boot/boot.s
    dd if=boot.bin of=hd.img seek=0 count=1 conv=notrunc
```

bochs配置文件：

```
memory: guest=32, host=32
boot: disk
ata0: enabled=1, ioaddr1=0x1f0, ioaddr2=0x3f0, irq=14
ata0-master: type=disk, mode=flat, path="hd.img"
```

在对照《Linux0.12内核完全完全剖析》敲代码时，遇到第SETUP 4个扇区总是加载不上，返回会0x80驱动器超时的错误。终于发现书中是从软盘上读取的，而自己的实验是从硬盘读取。

原书指定的驱动器是软盘：

```
load_setup:
    xor dx, dx
```

实际采用的是硬盘：

```
# 第二步：加载1-5共4个扇区的setup代码
load_setup:
    mov dx, #0x0080 ! DL＝驱动器 00H~7FH软盘 80H~0FFH硬盘
```

还有在使用dd前没有新建flat格式的hd.img硬盘文件。

参考：

[Ubuntu 16.04LTS 安装和配置Bochs](https://www.cnblogs.com/lfri/p/11489223.html)
